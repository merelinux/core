#!/bin/bash
# shellcheck disable=SC2034,SC2154

pkgname=base-layout
pkgver=2.0.7
pkgrel=1
pkgdesc='The base directory structure and a few core files for the system.'
arch=(any)
url='http://www.merelinux.org'
license=(MIT)
groups=(base)
depends=()
makedepends=()
options=(emptydirs)
changelog=ChangeLog
source=(
    passwd
    group
    profile
    shells
    services
    protocols
    shadow
)
sha256sums=(
    8d9729ffe6ad542e45bd9c2b43228f1e31c20a75721e8d34ec053cfef62bd985
    8ba0c6ce6030e0161f480eb8f9168451d5ce49e48cdd5b69525e443f411c20bd
    fc51d9783a4b429606e2621605ad9836d585fea60d051a7a0e8e62dd03d4bb45
    1dcc1e00d7189fa105c726b253aab416b3c0d47e432ac9958db54c7faacb9ac7
    b4d36eb75767bebb41c5fa7a35599952e1883b3d3f6332496e1561eeb4067018
    297b784a25fc59641589c6ef05dc26680e2805e9cab37a4ea3699aa072a25c2e
    6979dc53ed05ebdacc18700025ccf0232e0985f52aa56d31a5515935e03b04eb
)

backup=(
    etc/passwd
    etc/group
    etc/profile
    etc/shadow
    etc/shells
    etc/services
    etc/protocols
)

package() {
    install -d "${pkgdir}/bin"
    install -d "${pkgdir}/boot"
    install -d "${pkgdir}/dev"
    install -d "${pkgdir}/etc/default"
    ln -s /proc/mounts "${pkgdir}/etc/mtab"
    install -d "${pkgdir}/home"
    install -d "${pkgdir}/lib/modules"
    install -d "${pkgdir}/mnt"
    install -d -m 0555 "${pkgdir}/proc"
    install -d -m 0750 "${pkgdir}/root"
    install -d -m 0755 "${pkgdir}/run"
    install -d "${pkgdir}/sbin"
    install -d -m 0555 "${pkgdir}/sys"
    install -d -m 1777 "${pkgdir}/tmp"
    install -d "${pkgdir}/srv"
    install -d "${pkgdir}/usr/"{bin,include,local/bin,local/sbin,sbin,share/man}
    install -d "${pkgdir}/var/"{cache,lock,lib,log,mail,spool}
    ln -s /run "${pkgdir}/var/run"
    install -d -m 1777 "${pkgdir}/var/tmp"
    install -m0644 passwd    "${pkgdir}/etc/passwd"
    install -m0400 shadow    "${pkgdir}/etc/shadow"
    install -m0644 group     "${pkgdir}/etc/group"
    install -m0644 profile   "${pkgdir}/etc/profile"
    install -m0644 shells    "${pkgdir}/etc/shells"
    install -m0644 services  "${pkgdir}/etc/services"
    install -m0644 protocols "${pkgdir}/etc/protocols"
}
