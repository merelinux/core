#!/bin/bash
# shellcheck disable=SC2034,SC2154,SC2068

pkgname=(libpsl libpsl-dev)
pkgver=0.21.5
pkgrel=1
pkgdesc='C library for the Public Suffix List'
arch=(x86_64)
url='https://github.com/rockdaboot/libpsl'
license=(MIT)
groups=()
depends=()
makedepends=(
    meson
    ninja
)
options=()
changelog=ChangeLog

source=(
    "https://github.com/rockdaboot/libpsl/releases/download/${pkgver}/libpsl-${pkgver}.tar.gz"
)
sha256sums=(
    1dcc9ceae8b128f3c0b3f654decd0e1e891afc6ff81098f227ef260449dae208
)


build() {
    cd_unpacked_src
    meson build/ \
        -Dprefix=/usr \
        -Dbuildtype=release \
        -Ddefault_library=both
    ninja -C build/
}

package_libpsl() {
    depends=(
        "ld-musl-$(arch).so.1"
    )
    pkgfiles=(
        usr/lib/libpsl.so.5
    )
    provides=(
        libpsl.so.5
    )
    cd_unpacked_src
    DESTDIR="${pkgdirbase}/dest" ninja -C build/ install
    std_split_package
}

package_libpsl-dev() {
    depends=(
        "libpsl=${pkgver}"
    )
    pkgfiles=(
        usr/include
        usr/lib/lib*.a
        usr/lib/lib*.so
        usr/lib/pkgconfig
    )
    std_split_package
}
